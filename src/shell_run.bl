shell_run_mode_init :: fn () {
	active_editor = command_editor;
	buf :: get_active_buffer_and_editor();
	clear_buffer(buf);

	input_mode               = InputMode.TEXT;
	notify_changes_to_buffer = buf.index;

	list_history(history);
}

shell_run_mode_terminate :: fn () {}

shell_run_mode_on_start :: fn () {
	string_cache = pool_make(null, 2048);
}

shell_run_mode_on_exit :: fn () {
	release_allocator(&string_cache);
	array_terminate(&history);
}

draw_shell_run_mode :: fn () {
	assert(active_editor == command_editor);
	w, h :: get_command_editor_size();

	draw_command_editor(command_editor, 0.f, 0.f, w, h, "Shell: ");

	using DrawListCols;
	list_h, clicked :: draw_list(0.f, h, w, TITLE);
	draw_last_status_message(0.f, h + list_h, w);
	if clicked {
		set_current_item_as_caption();
		commit();
	}
}

//
// Commands
//

invoke_command_shell_run_mode :: fn (buf: *Buffer #maybe_unused, cmd: Command) bool {
	assert(active_editor == command_editor);
	assert(editors[active_editor].buffer_index == buf.index);

	using Command;

	switch cmd {
		INSERT_NEW_LINE { commit(); }

		MOVE_UP   {
			if is_current_selected_same_as_command() then list_item_up();
			set_current_item_as_caption();
		}

		MOVE_DOWN {
			if is_current_selected_same_as_command() then list_item_down();
			set_current_item_as_caption();
		}

		default { return false; }
	}

	return true;
}

shell_run_cmd_changed :: fn () {
	buf :: get_active_buffer_and_editor();
	cmd :: peek_row(buf, 0);
	if cmd.len == 0 {
		clear_list_filters();
		return;
	}
	filter_list(cmd);
}

#scope_private

string_cache: PoolAllocator;
history: [..]HistoryItem;
current_priority: s32;

commit :: fn () {
	buf :: get_active_buffer_and_editor();
	cmd := peek_row(buf, 0);
	set_mode(Mode.TEXT_EDIT);

	trim_chars :: " \t";
	cmd = trim_right(trim_left(cmd, trim_chars), trim_chars);
	
	if cmd.len == 0 then return;

	err :: request_build(cmd);
	if err {
		print_err(err);
	} else {
		index :: lookup_history_item(cmd);
		if index == -1 {
			array_push(&history, .{ str_new(cmd, &string_cache), current_priority });
		} else {
			history[index].priority = current_priority;
		}
		current_priority += 1;
	}
}

lookup_history_item :: fn (title: string_view) s32 {
	loop i := 0; i < history.len; i += 1 {
		if str_match(title, history[i].title) {
			return i;
		}
	}
	return -1;
}

is_current_selected_same_as_command :: fn () bool {
	item :: get_selected_item_in_list();
	if !item then return false;

	buf :: get_active_buffer_and_editor();
	current_cmd := peek_row(buf, 0);
	return str_match(current_cmd, item.title);
}

set_current_item_as_caption :: fn () {
	item :: get_selected_item_in_list();
	if !item then return;
	
	buf :: get_active_buffer_and_editor();
	notify_changes_to_buffer = -1;
	clear_buffer(buf);
	insert_string(buf, 0, item.title);
	notify_changes_to_buffer = buf.index;
}