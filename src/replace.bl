replace_mode_init :: fn () {
	active_editor = command_editor;
	buf, editor :: get_active_buffer_and_editor();
	clear_buffer(buf);

	input_mode                = InputMode.TEXT;
	notify_changes_to_buffer  = buf.index;
	is_replacing              = false;
	last_replacement_position = -1;

	// Show last search in the command buffer.
	if search.last_replacement.len > 0 {
		insert_string(buf, 0, search.last_replacement);
		select_all(editor, buf);
	}

	// @Incomplete: use search.last_replacement

	assert(search.last_subject.len > 0);
	focus_current_search_result();

	if settings.show_hints {
		using Command;
		replace_binding :: get_keybinding_for_command(INSERT_NEW_LINE);
		next_binding    :: get_keybinding_for_command(MOVE_DOWN);
		prev_binding    :: get_keybinding_for_command(MOVE_UP);
		all_binding     :: get_keybinding_for_command(SELECT_ALL);
		hint :: tprint("[%] next match\t[%] prev match\t[%] commit replacement\t[%] replace all", next_binding, prev_binding, replace_binding, all_binding);
		print_info(hint);
	}
}

replace_mode_terminate :: fn () {
	if last_replacement_position != -1 {
		target_buf, target_editor :: get_last_active_buffer_and_editor();
		set_cursor_position(target_buf, target_editor.cursor_index, last_replacement_position);
	}
}

draw_replace_mode :: fn () {
	assert(active_editor == command_editor);
	assert(search.last_subject.len > 0);
	w, h :: get_command_editor_size();
	target_buf :: get_last_active_buffer_and_editor();

	if is_replacing {
		using draw;
		using Metric;

		line_height  :: metrics[auto LINE_HEIGHT ];
		text_padding :: metrics[auto TEXT_PADDING];

		set_shader_color();
		draw_shadow(0.f, 0.f, w, h, ShadowMode.BOTTOM);
		rect(0.f, 0.f, w, h, colors.command_background_color);

		title_x :: text_padding;
		title_y :: text_padding + (line_height * 0.5f - font.height * 0.5f);

		title :: tprint("Replacing '%' with '%'.", search.last_subject, search.last_replacement);

		set_shader_font(&font);
		text(title_x, title_y, title, colors.command_title_color);
	} else {
		draw_command_editor(command_editor, 0.f, 0.f, w, h, tprint("Replace '%' in '%' with: ", search.last_subject, target_buf.name));
	}
	draw_last_status_message(0.f, h, w);
}

invoke_command_replace_mode :: fn (buf: *Buffer #maybe_unused, cmd: Command) bool {
	assert(active_editor == command_editor);
	assert(editors[active_editor].buffer_index == buf.index);

	target_buf, target_editor :: get_last_active_buffer_and_editor();

	subject     : string_view : search.last_subject;
	replacement : string_view = search.last_replacement;

	using Command;
	switch cmd {
		MOVE_UP   { prev_match(target_buf, target_editor); }
		MOVE_DOWN { next_match(target_buf, target_editor); }

		INSERT_NEW_LINE {
			if is_replacing {
				assert(subject.len > 0);

				start :: get_cursor_position(target_buf, target_editor.cursor_index);
				end   :: start + subject.len;

				last_replacement_position = start;

				batch_begin(target_buf);
				delete_range(target_buf, start, end);
				if replacement.len > 0 { // Empty is valid!
					insert_string(target_buf, start, replacement);
				}
				batch_end(target_buf);

				diff :: cast(s32) (replacement.len - subject.len);
				loop i := search.result_index; i < search.results.len; i += 1 {
					r :: &search.results[i];
					r.start += diff;
					r.end   += diff;
					// @Incomplete: Adjust col and row???
				}

				array_erase_keep_order(&search.results, search.result_index);
				if search.result_index >= search.results.len {
					search.result_index = 0;
				}

				// Jumps to next one since previous entry was deleted.
				if !focus_current_search_result() {
					// Last match
					set_mode(Mode.TEXT_EDIT);
					print_info("All replaced.");
				}
			} else {
				commit_current_replacement();
				is_replacing = true;

				if settings.show_hints {
					using Command;
					replace_binding :: get_keybinding_for_command(INSERT_NEW_LINE);
					next_binding    :: get_keybinding_for_command(MOVE_DOWN);
					prev_binding    :: get_keybinding_for_command(MOVE_UP);
					all_binding     :: get_keybinding_for_command(SELECT_ALL);
					hint :: tprint("[%] next match\t[%] prev match\t[%] replace\t[%] replace all", next_binding, prev_binding, replace_binding, all_binding);
					print_info(hint);
				}
			}
		}

		SELECT_ALL {
			if !is_replacing {
				replacement  = commit_current_replacement();
				is_replacing = true;
			}
			assert(subject.len > 0);
			diff :: cast(s32) (replacement.len - subject.len);
			loop i := 0; i < search.results.len; i += 1 {
				r :: &search.results[i];
				start :: r.start + diff * i;
				end   :: r.end   + diff * i;
				batch_begin(target_buf);
				delete_range(target_buf, start, end);
				if replacement.len > 0 { // Empty is valid!
					insert_string(target_buf, start, replacement);
				}
				batch_end(target_buf);
				if i + 1 == search.results.len {
					set_cursor_position(target_buf, target_editor.cursor_index, start + replacement.len);
				}
			}
			search.results.len = 0;
			set_mode(Mode.TEXT_EDIT);
			print_info("All replaced.");
		}

		default { return false; }
	}

	return true;
}

#private

is_replacing: bool;
last_replacement_position: s64;

commit_current_replacement :: fn () string_view #inline {
	buf :: get_active_buffer_and_editor();
	replacement :: peek_row(buf, 0);
	search.last_replacement.len = 0;

	if replacement.len > 0 {
		std.str_append(&search.last_replacement, replacement);
	}

	return search.last_replacement;
}
