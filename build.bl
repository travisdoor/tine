#import "extra/argparse"
#import "std/print"
#import "std/fs"
#import "std/string"

Args :: struct {
	is_release: bool;
}

args: Args;

build :: fn () #build_entry {
	defer temporary_release();

	opt := get_builder_options();
	opt.full_path_reports = true;
	set_builder_options(opt);

	parser: Parser;
	init(&parser, "The Editor.");
	defer terminate(&parser);

	set_executable_name(&parser, "blc -build");
	add_usage(&parser, "[options]");

	add(&parser, &args.is_release, "--release", "", "Compile in release mode.");

	_, parse_error :: parse(&parser, command_line_arguments, 0);
	if parse_error {
		print_err(parse_error);
		print_help(&parser);
		return;
	}

	if parser.help {
		return;
	}

	exe: *Target;

	if args.is_release {
		exe = add_executable("Tine");
		exe.build_mode = BuildMode.RELEASE_FAST;
		// exe.build_mode = BuildMode.RELEASE_WITH_DEBUG_INFO;
	} else {
		exe = add_executable("Tine-Debug");
	}
	set_module_dir(exe, "modules");
	add_unit(exe, "src/main.bl");

	#if PLATFORM == Platform.WINDOWS {
		add_unit(exe, "src/platform/win32.bl");
		if args.is_release {
			append_linker_options(exe, "/SUBSYSTEM:WINDOWS");
		}
	} else if PLATFORM == Platform.DARWIN {
		add_unit(exe, "src/platform/darwin.bl");
		// append_linker_options(exe, "-framework CoreFoundation -framework CoreServices");
	} else if PLATFORM == Platform.LINUX {
		add_unit(exe, "src/platform/linux.bl");
	}

	compile(exe) catch panic($);

	if args.is_release {
		if PLATFORM == Platform.WINDOWS {
			shell("icon\\rcedit.exe", "\"Tine.exe\"", "--set-icon \"icon\\icon.ico\"");
		} else if PLATFORM == Platform.DARWIN {
			create_macos_bundle();
		}
	}
}

create_macos_bundle :: fn () {
	app_dir :: "tine-macos-arm64";
	shell("rm", "-rf", app_dir);
	create_all_dir(tprint("%/Tine.app/Contents/MacOS", app_dir));
	create_all_dir(tprint("%/Tine.app/Contents/Resources", app_dir));
	copy_file("Tine", tprint("%/Tine.app/Contents/MacOS/Tine", app_dir));
	copy_file("Info.plist", tprint("%/Tine.app/Contents/Info.plist", app_dir));
	copy_file("icon/tine.icns", tprint("%/Tine.app/Contents/Resources/tine.icns", app_dir));

	modules_dir :: get_default_module_dir();

	shell(
		"dylibbundler",
		"-b",
		tprint("-d %/Tine.app/Contents/MacOS", app_dir),
		"-p @executable_path/",
		tprint("-x %/Tine.app/Contents/MacOS/Tine", app_dir),
		"-s ./modules/pcre/macos",
		tprint("-s %/extra/sdl3/macos", modules_dir),
		tprint("-s %/extra/freetype2/macos", modules_dir),
		tprint("-s %/extra/png/macos", modules_dir),
		tprint("-s %/extra/zlib/macos", modules_dir),
	);

	shell("chmod", "+x", tprint("%/Tine.app/Contents/MacOS/Tine", app_dir));
	shell("ln", "-s", "/Applications", tprint("%/Applications", app_dir));

	remove_file("Tine.dmg");
	shell(
		"hdiutil",
		"create",
		"-volname \"Tine\"",
		tprint("-srcfolder %", app_dir),
		"-format UDZO",
		"-o Tine.dmg",
	);
}

shell :: fn (exec: string_view, args: ...string_view) {
	tmp: string;
	str_append(&tmp, exec);
	loop i := 0; i < args.len; i += 1 {
		str_append(&tmp, " ");
		str_append(&tmp, args[i]);
	}

	print_info(tmp);
	os_execute(tmp);
}

